#----------------------------------------------------------------------------
# Set up project properties
cmake_minimum_required(VERSION 3.19)

project(ET VERSION @ET_VERSION@ LANGUAGES C)
string(TOLOWER ${PROJECT_NAME} PROJECT_NAME_LC)
string(TOUPPER ${PROJECT_NAME} PROJECT_NAME_UC)

#----------------------------------------------------------------------------
# Name(s) of the main item(s) built here
set(LIBNAME ${PROJECT_NAME_LC})

#----------------------------------------------------------------------------
# Sources and headers
set(src et_attachment.c et_bridge.c et_common.c et_data.c et_event.c
  et_init.c et_local.c et_mem.c et_network.c et_noshare.c et_openconfig.c
  et_readwrite.c et_remote.c et_server.c et_statconfig.c et_station.c
  et_sysconfig.c et_system.c etCommonNetwork.c et_fifo.c)
set(hdr et.h et_data.h et_private.h et_fifo.h et_network.h etCommonNetwork.h)

#----------------------------------------------------------------------------
# Library
add_library(${LIBNAME} SHARED ${src} ${hdr})
add_library(${PROJECT_NAME_UC}::${PROJECT_NAME} ALIAS ${LIBNAME})
set_property(TARGET ${LIBNAME} PROPERTY EXPORT_NAME ${PROJECT_NAME})

target_include_directories(${LIBNAME}
  PUBLIC
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
  )
target_compile_options(${LIBNAME}
  PRIVATE
    $<$<CONFIG:Debug>:-O0>
    $<$<CONFIG:RelWithDebInfo>:-fno-omit-frame-pointer>
    -Wall -Wextra -Wno-unused-parameter -Wno-unused-variable
  # Suppress numerous ET compilation warnings
    -Wno-sign-compare -Wno-unused-function -Wno-uninitialized
  )
set_target_properties(${LIBNAME} PROPERTIES
  SOVERSION ${PROJECT_VERSION_MAJOR}
  VERSION ${PROJECT_VERSION}
  )

if(NOT DEFINED CMAKE_INSTALL_LIBDIR)
  include(GNUInstallDirs)
endif()
install(TARGETS ${LIBNAME}
  EXPORT ${PROJECT_NAME}-exports
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
  )
install(FILES ${hdr} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

#----------------------------------------------------------------------------
# Create exports and project configuration file
set(${PROJECT_NAME_UC}_INSTALL_CONFIGDIR ${CMAKE_INSTALL_LIBDIR}/${PROJECT_NAME_LC})
set(_configdir ${${PROJECT_NAME_UC}_INSTALL_CONFIGDIR})

install(EXPORT ${PROJECT_NAME}-exports
  FILE ${PROJECT_NAME}Targets.cmake
  NAMESPACE ${PROJECT_NAME_UC}::
  DESTINATION ${_configdir}
  )

include(CMakePackageConfigHelpers)

set(TARGETS_FILE ${_configdir}/${PROJECT_NAME}Targets.cmake)
configure_package_config_file(
  ${PROJECT_NAME}Config.cmake.in
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake
  INSTALL_DESTINATION ${_configdir}
  PATH_VARS
    CMAKE_INSTALL_PREFIX
    ${PROJECT_NAME_UC}_INSTALL_CONFIGDIR
    TARGETS_FILE
  NO_SET_AND_CHECK_MACRO
  NO_CHECK_REQUIRED_COMPONENTS_MACRO
  )

write_basic_package_version_file(
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake
  VERSION ${PROJECT_VERSION}
  COMPATIBILITY SameMajorVersion
  )

install(FILES
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake
  DESTINATION ${_configdir}
  )

unset(_configdir)
